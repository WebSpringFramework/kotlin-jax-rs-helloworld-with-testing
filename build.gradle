/**
 * This section is for all of the plugins we need to make this work.
 * Kotlin, the fat jar builder, and flag it
 * as an application so `./gradlew run` will work
 */
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.41'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'application'
apply plugin: 'idea'

/**
 * Standard dependency section for gradle.
 * Define the kotlin standard library for
 * Java 8.
 */
repositories {
    jcenter()
    maven {
        url  "https://dl.bintray.com/mockito/maven"
    }
}

def jerseyVersion = "2.27"
def junitVersion = "5.2.0"

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile "org.glassfish.jersey.containers:jersey-container-grizzly2-http:${jerseyVersion}"
    compile "org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}"

    // junit dependencies
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntime "org.junit.vintage:junit-vintage-engine:${junitVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    // jersey test dependencies
    testCompile "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:${jerseyVersion}"

    // assertJ
    testCompile 'org.assertj:assertj-core:3.10.0'

    // mockito
    testCompile 'org.mockito:mockito-core:2.18.3'
    testCompile 'org.mockito:mockito-junit-jupiter:2.18.3'
    testCompile 'com.nhaarman:mockito-kotlin:1.5.0'
}

test {
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

/**
 * Personal preference. I hate having src/main/kotlin
 * be the root, so I change it that 'src'
 * as the root of my source directory.
 */
sourceSets {
    main.kotlin {
        srcDirs += 'src'
        exclude "**/*.tests.kt"
    }

    test.kotlin {
        srcDirs += 'src'
        include "**/*.tests.kt"
    }
}

// Define the main startup class and jar name
mainClassName = 'ServerKt'
archivesBaseName = 'step-by-step-kotlin-hello-world-jaxrs'

// tell the jar which class to startup in.
jar {
    manifest {
        attributes 'Main-Class': 'ServerKt'
    }
}

// Properties for `./gradlew run`
run {
    standardInput = System.in
    environment = ["SHUTDOWN_TYPE" : "INPUT"]
}

// This is here to fix the intellij's issues with
// the complicated source mappings. Use the 'idea' job
// to generate a intellij project
// Ticket to make this just work out of the box: https://youtrack.jetbrains.com/issue/IDEA-188436
idea {
    module {
        scopes.COMPILE.plus += [ configurations.testCompile ]
    }
}
